buildscript {
    ext["hibernate.version"] = "5.2.10.Final" 
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE"
    }
}

apply plugin: "org.springframework.boot"

version = "1.3.6"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

javadoc {
    options.encoding = defaultEncoding
    options.addBooleanOption('Xdoclint:none', true)
}

compileJava.dependsOn(processResources)

test {
    include "**/model/**", "**/controller/**", "**/usecase/**", "**/util/**", "**/context/**"
}

springBoot {
    executable = true
    buildInfo()
}

repositories {
    jcenter()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.18"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.ehcache:ehcache:3.2.+"
    compile "javax.cache:cache-api:1.0.0"
    compile "com.zaxxer:HikariCP:2.6.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.9.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.+"
    compile "org.eclipse.collections:eclipse-collections:8.2.0"
    compile "org.eclipse.collections:eclipse-collections-api:8.2.0"
    compile "commons-io:commons-io:2.5"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "com.ibm.icu:icu4j:59.1"
    compile fileTree(dir: 'libs', includes: ['*.jar'])
    runtime "com.h2database:h2:1.4.+"
    testCompileOnly "org.projectlombok:lombok:1.16.18"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.1"
}

// After when you wanted to make deep copying as one's project, having edited the following variables
// and execute "gradlew copyProject".
def yourProjectName = "sample"
def yourProjectPackage = "your.sample"
def yourProjectPath = "../${yourProjectName}"
task copyProject {
    def projectPath = yourProjectPath
    def packagePath = yourProjectPackage.replaceAll(/\./, "/")
    def javaPath = "${projectPath}/src/main/java/${packagePath}"
    def testPath = "${projectPath}/src/test/java/${packagePath}"
    copy {
        from (".") { exclude ".*", "bin", "build", "LICENSE", "src/main/java", "src/test/java" }
        into projectPath
    }
    ant.copy(file: ".gitignore", todir: "${projectPath}")
    copy {
        from ("src/main/java/sample")
        into javaPath
    }
    copy {
        from ("src/test/java/sample")
        into testPath
    }
    ant.replace(token: "sample-boot-hibernate", value: yourProjectName) {
        fileset(dir: projectPath) {
            include(name: "README.md")
            include(name: "settings.gradle")
            include(name: "src/main/resources/application.yml")
            include(name: "src/main/resources/banner.txt")
        }
    }
    ant.replace(token: "version = \"${version}\"", value: "version = \"1.0.0\"") {
        fileset(dir: projectPath) { include(name: "build.gradle") }
    }
    ant.replace(token: "package-to-scan: sample", value: "package-to-scan: ${yourProjectPackage}") {
        fileset(dir: projectPath) { include(name: "src/main/resources/application.yml") }
    }
    ant.replace(token: "logger name=\"sample\"", value: "logger name=\"${yourProjectPackage}\"") {
        fileset(dir: projectPath) { include(name: "src/main/resources/logback-spring.xml") }
        fileset(dir: projectPath) { include(name: "src/test/resources/logback.xml") }
    }
    ant.replace(token: "import sample", value: "import ${yourProjectPackage}") {
        fileset(dir: javaPath) { include(name: "**/*.java") }
        fileset(dir: testPath) { include(name: "**/*.java") }
    }
    ant.replace(token: "package sample", value: "package ${yourProjectPackage}") {
        fileset(dir: javaPath) { include(name: "**/*.java") }
        fileset(dir: testPath) { include(name: "**/*.java") }
    }
    ant.replace(token: "PackageRoot = \"sample\"", value: "PackageRoot = \"${yourProjectPackage}\"") {
        fileset(dir: testPath) { include(name: "**/DdlExporter.java") }
    }
    ant.replace(token: "packageToScan = \"sample\"", value: "packageToScan = \"${yourProjectPackage}\"") {
        fileset(dir: testPath) { include(name: "**/EntityTestSupport.java") }
    }    
}
